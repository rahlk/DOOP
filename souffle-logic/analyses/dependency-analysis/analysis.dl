#include "../2-object-sensitive+heap/analysis.dl"
#include "declarations.dl"
#include "../data-flow/analysis.dl"

HeapCarriedDependencyFlows(?ctx, ?meth1, ?meth2),
TaintedLoad(?ctx, ?dependent, ?insn) :-
    StoreFldTo(?var, ?fld, ?dependent),
    LoadFldFrom(?insn, ?var2, ?fld),
    mainAnalysis.VarPointsTo(?hctx, ?value, ?ctx, ?var),
    mainAnalysis.VarPointsTo(?hctx, ?value, ?ctx, ?var2),
    Instruction_Method(?dependent, ?meth1),
    Instruction_Method(?insn, ?meth2).

HeapCarriedDependencyFlows(?ctx, ?meth1, ?meth2),
TaintedLoad(?ctx, ?dependent, ?insn) :-
    LoadArrayIndex_Base(?insn, ?var),
    StoreArrayIndex_Base(?dependent, ?var1),
    mainAnalysis.VarPointsTo(?hctx, ?value, ?ctx, ?var),
    mainAnalysis.VarPointsTo(?hctx, ?value, ?ctx, ?var1),
    Instruction_Method(?dependent, ?meth1),
    Instruction_Method(?insn, ?meth2).

CallDependency(?nextCtx, ?prevMeth, ?prevCtx, ?meth):- 
    mainAnalysis.CallGraphEdge(?prevCtx, ?callsite, ?nextCtx, ?meth),
    Instruction_Method(?callsite, ?prevMeth),
    !SecureMethod(?meth).

/* Data dependency between the formal and actual param */
CallDependency(?nextCtx, ?prevMeth, ?prevCtx, ?meth), 
CallReturnDependency(?nextCtx, ?nextInsn, ?prevCtx, ?prevInsn) :-
    mainAnalysis.CallGraphEdge(?prevCtx, ?callsite, ?nextCtx, ?meth),
    ActualParam(?paramIndex, ?callsite, ?actParam),
    FormalParam(?paramIndex, ?meth, ?formalParam),
    InstructionDefinesVar(?prevInsn, ?actParam),
    InstructionUsesVar(?nextInsn, ?formalParam),
    Instruction_Method(?prevInsn, ?prevMeth),
    Instruction_Method(?nextInsn, ?meth),
    !SecureMethod(?meth).

/* Data dependency between the return instruction and the caller */
ReturnDependency(?nextCtx, ?meth, ?prevCtx, ?nextMeth), 
CallReturnDependency(?nextCtx, ?nextInsn, ?prevCtx, ?prevInsn) :-
    isReturnNonvoid_Insn(?prevInsn),
    Instruction_Method(?prevInsn, ?meth),
    mainAnalysis.CallGraphEdge(?nextCtx, ?nextInsn, ?prevCtx, ?meth),
    Instruction_Method(?nextInsn, ?nextMeth).

/* All kinds of intra-procedural dependency */
DependentInstruction(?ctx, ?prevInsn, ?curInsn)  :-
    // DependentInstruction(?prevCtx, ?prevInsn, ?curInsn),
    Instruction_Method(?prevInsn, ?meth),
    IntraProceduralDependency(?prevInsn, ?curInsn),
    mainAnalysis.ReachableContext(?ctx, ?meth),
    !SecureInstruction(?prevInsn),
    !SecureInstruction(?curInsn).

/**
 * If an instruction is inter-procedurally dependent on a dependent instruction
 * mark that instruction as dependent in its context
 */
DependentInstruction(?ctx, ?prevInsn, ?curInsn) :-
    DependentInstruction(?prevCtx, ?prevInsn, ?someInsn),
    CallReturnDependency(?ctx, ?curInsn, ?prevCtx, ?someInsn).

/**
 * If a method call has a control dependency on an instruction, every instruction
 * in that method is control dependent on that instruction
 */
DependentInstruction(?ctx, ?prevInsn, ?curInsn) :-
    DependentInstruction(?prevCtx, ?prevInsnControlDep, _),
    IntraProceduralControlDep(?prevInsn, ?prevInsnControlDep),
    mainAnalysis.CallGraphEdge(?prevCtx, ?prevInsn, ?ctx, ?meth),
    Instruction_Method(?curInsn, ?meth).

/* Insn stores to base.fld */
StoreFldTo(?base, ?fld, ?insn) :-
    StoreInstanceField_Base(?insn, ?base),
    FieldInstruction_Signature(?insn, ?fld).

/* Insn loads from base.fld */
LoadFldFrom(?insn, ?base, ?fld) :-
    LoadInstanceField_Base(?insn, ?base),
    FieldInstruction_Signature(?insn, ?fld).

/* Var is in instruction's use set */
InstructionUsesVar(?instruction, ?var) :-
    AssignLocal_From(?instruction, ?var) ;
    AssignOper_From(?instruction, _, ?var) ;
    AssignCast_From(?instruction, ?var) ;
    AssignInstanceOf_From(?instruction, ?var) ;
    If_Var(?instruction, _, ?var) ;
    VirtualMethodInvocation_Base(?instruction, ?var) ;
    SuperMethodInvocation_Base(?instruction, ?var) ;
    Switch_Key(?instruction, ?var) ;
    ActualParam(_, ?instruction, ?var) ;
    ReturnNonvoid_Var(?instruction, ?var) ;
    Throw_Var(?instruction, ?var) ;
    LoadArrayIndex_Base(?instruction, ?var) ;
    StoreStaticField_From(?instruction, ?var) ;
    StoreInstanceField_From(?instruction, ?var) ;
    StoreArrayIndex_From(?instruction, ?var) ;
    ArrayInsnIndex(?instruction, ?var).

/* Var is in instruction's def set */
InstructionDefinesVar(?instruction, ?var) :-
    AssignInstruction_To(?instruction, ?var) ;
    LoadArrayIndex_To(?instruction, ?var) ;
    StoreArrayIndex_Base(?instruction, ?var) ;
    LoadInstanceField_To(?instruction,  ?var) ;
    LoadStaticField_To(?instruction,  ?var) ;
    AssignReturnValue(?instruction, ?var).


/* An instruction is a CFG leaf if it ends with a throw */
CFGLeaf(?headInsn, ?method) :-
    Throw(?insn, _),
    BasicBlockHead(?insn, ?headInsn),
    Instruction_Method(?headInsn, ?method).

/* An instruction is a CFG leaf if it ends with a return */
CFGLeaf(?headInsn, ?method) :-
    isReturnInstruction(?insn),
    BasicBlockHead(?insn, ?headInsn),
    Instruction_Method(?headInsn, ?method).


/* Nothing post-dominates a CFG leaf */
DoesNotPostDominate(?postDomCandidate, ?insn) :-
    BBHeadInMethod(?postDomCandidate, ?method),
    CFGLeaf(?insn, ?method),
    ?postDomCandidate != ?insn.

/**
 * If A may come before B and C does not post-dominate B,
 * then C does not post-dominate A either, as after A the execution may go to B.
 * This is a lot more intuitive when one thinks that "DoesNotPostDominate(a,b) =
 * exists path from b to some exit of the method, such that the path skips a".
 */
DoesNotPostDominate(?postDomCandidate, ?insn) :-
    DoesNotPostDominate(?postDomCandidate, ?otherInsn),
    MayPredecessorBBModuloThrow(?insn, ?otherInsn),
    ?insn != ?postDomCandidate.


/* If we can't prove A does not post-dominate B with the other rules, then A post-dominates B.
 * The definition is such that we always get reflexivity: PostDominates(x,x).
 */
PostDominates(?dominator, ?insn) :-
    SameMethodBBHeads(?dominator, ?insn),
    !DoesNotPostDominate(?dominator, ?insn).


/* An intra procedural data dep exists when an instruction uses a var defined by another one */
IntraProceduralDataDep(?prev, ?next) :-
    InstructionDefinesVar(?prev, ?var),
    InstructionUsesVar(?next, ?var).


/* This block may follow an instruction, but it doesn't postdominate it.
 * Invariant of this relation: ?prev is the
 * last instruction of a BB, ?nextBlock is the first of a different one.
 */
IntraProceduralBlockControlDep(?nextBlock, ?prev) :-
    BasicBlockBegin(?nextBlock),
    MaySuccessorModuloThrow(?nextBlock, ?prev),
    BasicBlockHead(?prev, ?prevBlockStart),
    !PostDominates(?nextBlock, ?prevBlockStart).

/**
 * This block postdominates a block that depends on an instruction
 * which this block doesn't postdominate
 */
IntraProceduralBlockControlDep(?nextBlock, ?prev) :-
    BasicBlockHead(?prev, ?prevBlockStart),
    IntraProceduralBlockControlDep(?interm, ?prev),
    PostDominates(?nextBlock, ?interm),
    !PostDominates(?nextBlock, ?prevBlockStart).


/* Same as above, but per instruction */
IntraProceduralControlDep(?next, ?prev) :-
    IntraProceduralBlockControlDep(?nextBlock, ?prev),
    BasicBlockHead(?next, ?nextBlock).


/* Intra procedural dependency base */
IntraProceduralDependencyBase(?next, ?prev) :-
    IntraProceduralDataDep(?prev, ?next)/* ;
    IntraProceduralControlDep(?next, ?prev)*/.

/* Intra procedural dependency - base case */
IntraProceduralDependency(?prev, ?next) :-
    IntraProceduralDependencyBase(?next, ?prev).

/* Intra procedural dependency step */
IntraProceduralDependency(?prev, ?next) :-
    IntraProceduralDependency(?prev, ?inter),
    IntraProceduralDependencyBase(?next, ?inter).


/**
 * ========================
 * Additional Logic for OLA
 * ========================
 */


CallGraphEdgeBetweenMethods(?cctx, ?meth1, ?meth2) :- 
    Instruction_Method(?callSite, ?meth1),
    mainAnalysis.CallGraphEdge(?prevCtx, ?callSite, ?cctx, ?meth2).

HeapCarriedDependencyFlows(?ctx, ?meth1, ?meth2) :- 
    InstructionUsesVar(?insn1, ?var1),
    InstructionUsesVar(?insn2, ?var2),
    mainAnalysis.VarPointsTo(?hctx, ?heap, ?ctx, ?var1),
    mainAnalysis.VarPointsTo(?hctx, ?heap, ?ctx, ?var2),
    Instruction_Method(?insn1, ?meth1),
    Instruction_Method(?insn2, ?meth2),
    CallGraphEdgeBetweenMethods(?ctx, ?meth1, ?meth2),
    mainAnalysis.ReachableContext(?ctx, ?meth1),
    mainAnalysis.ReachableContext(?ctx, ?meth2),
    ?meth1 != ?meth2.

HeapCarriedDependencyFlows(?ctx, ?meth1, ?meth2) :-
    (InstructionUsesVar(?insn1, ?var1); 
    InstructionDefinesVar(?insn1, ?var1)),
    InstructionUsesVar(?insn2, ?var2),
    Flows(?var1, ?var2),
    mainAnalysis.VarPointsTo(?hctx, ?heap, ?ctx, ?var1),
    mainAnalysis.VarPointsTo(?hctx, ?heap, ?ctx, ?var2),
    Instruction_Method(?insn1, ?meth1),
    Instruction_Method(?insn2, ?meth2),
    CallGraphEdgeBetweenMethods(?ctx, ?meth1, ?meth2),
    mainAnalysis.ReachableContext(?ctx, ?meth1),
    mainAnalysis.ReachableContext(?ctx, ?meth2),
    ?meth1 != ?meth2.

 // Interprocedural Instruction Depenencies 
 // ---------------------------------------

DependentInstructionsBetweenMethods(?dctx, ?prev, ?next) :- 
    DependentInstruction(?dctx, ?insn1, ?insn2),
    Instruction_Method(?insn1, ?prev),
    Instruction_Method(?insn2, ?next),
    !SecureInstruction(?insn1),
    !SecureInstruction(?insn2),
    ?prev != ?next.